#version 450
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_KHR_shader_subgroup_shuffle : require

layout (set = 0, binding = 0, rgba32f) uniform imageCube mipImages[];

layout (push_constant) uniform PushConstant {
    uint baseLevel;
    uint remainingMipLevels;
} pc;

layout (local_size_x = 16, local_size_y = 16) in;

shared vec4 sharedData[16];

void main(){
    ivec2 sampleCoordinate = ivec2(gl_WorkGroupSize.xy * gl_WorkGroupID.xy + uvec2(
        (gl_LocalInvocationID.x & 3U) | (gl_LocalInvocationID.y & ~3U),
        ((gl_LocalInvocationID.y << 2U) | (gl_LocalInvocationID.x >> 2U)) & 15U
    ));

    vec4 averageColor
        = imageLoad(mipImages[pc.baseLevel], ivec3(2 * sampleCoordinate, gl_GlobalInvocationID.z))
        + imageLoad(mipImages[pc.baseLevel], ivec3(2 * sampleCoordinate + ivec2(1, 0), gl_GlobalInvocationID.z))
        + imageLoad(mipImages[pc.baseLevel], ivec3(2 * sampleCoordinate + ivec2(0, 1), gl_GlobalInvocationID.z))
        + imageLoad(mipImages[pc.baseLevel], ivec3(2 * sampleCoordinate + ivec2(1, 1), gl_GlobalInvocationID.z));
    averageColor /= 4.0;
    imageStore(mipImages[pc.baseLevel + 1U], ivec3(sampleCoordinate, gl_GlobalInvocationID.z), averageColor);
    if (pc.remainingMipLevels == 1U){
        return;
    }

    averageColor += subgroupShuffleXor(averageColor, 1U /* 0b0001 */);
    averageColor += subgroupShuffleXor(averageColor, 4U /* 0b0100 */);
    averageColor /= 4.f;
    if ((gl_SubgroupInvocationID & 5U /* 0b101 */) == 5U) {
        imageStore(mipImages[pc.baseLevel + 2U], ivec3(sampleCoordinate >> 1, gl_GlobalInvocationID.z), averageColor);
    }
    if (pc.remainingMipLevels == 2U){
        return;
    }

    averageColor += subgroupShuffleXor(averageColor, 2U /* 0b0010 */);
    averageColor += subgroupShuffleXor(averageColor, 8U /* 0b1000 */);
    averageColor /= 4.f;

    if ((gl_SubgroupInvocationID & 15U /* 0b1111 */) == 15U) {
        imageStore(mipImages[pc.baseLevel + 3U], ivec3(sampleCoordinate >> 2, gl_GlobalInvocationID.z), averageColor);
    }
    if (pc.remainingMipLevels == 3U){
        return;
    }

    if (subgroupElect()){
        sharedData[gl_SubgroupID] = averageColor;
    }

    memoryBarrierShared();
    barrier();

    if ((gl_SubgroupID & 5U) == 5U){
        averageColor = (sharedData[gl_SubgroupID] + sharedData[gl_SubgroupID ^ 1U] + sharedData[gl_SubgroupID ^ 4U] + sharedData[gl_SubgroupID ^ 5U]) / 4.f;
        imageStore(mipImages[pc.baseLevel + 4U], ivec3(sampleCoordinate >> 3, gl_GlobalInvocationID.z), averageColor);
    }
    if (pc.remainingMipLevels == 4U){
        return;
    }

    if (gl_LocalInvocationIndex == 0U){
        averageColor = (sharedData[0] + sharedData[1] + sharedData[2] + sharedData[3] + sharedData[4] + sharedData[5] + sharedData[6] + sharedData[7] + sharedData[8] + sharedData[9] + sharedData[10] + sharedData[11] + sharedData[12] + sharedData[13] + sharedData[14] + sharedData[15]) / 16.f;
        imageStore(mipImages[pc.baseLevel + 5U], ivec3(sampleCoordinate >> 4, gl_GlobalInvocationID.z), averageColor);
    }
}