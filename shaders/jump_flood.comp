#version 460
#extension GL_GOOGLE_include_directive : require

#include "morton.glsl"

const uint UINT_MAX = 4294967295U;
const ivec2 normalizedOffsets[9] = {
    { -1, -1 }, { 0, -1 }, { 1, -1 },
    { -1,  0 }, { 0,  0 }, { 1,  0 },
    { -1,  1 }, { 0,  1 }, { 1,  1 }
};

layout (set = 0, binding = 0, rg16ui) uniform uimage2DArray pingPongImage;

layout (push_constant, std430) uniform PushConstant {
    bool forward;
    uint sampleOffset;
} pc;

layout (local_size_x = 16, local_size_y = 16) in;

uint length2(uvec2 v) {
    return v.x * v.x + v.y * v.y;
}

void main(){
    ivec3 centerSampleCoord = ivec3(
        (gl_WorkGroupID.xy << 4) | unmorton16(gl_LocalInvocationIndex),
        (pc.forward ? 0 : gl_NumWorkGroups.z) | gl_GlobalInvocationID.z);

    uvec2 imageExtent = imageSize(pingPongImage).xy;
    if (centerSampleCoord.x >= imageExtent.x || centerSampleCoord.y >= imageExtent.y) {
        return;
    }

    uvec2 closestSeedCoord;
    uint closestSeedDistanceSq = UINT_MAX;
    for (uint i = 0; i < 9; ++i){
        ivec3 sampleCoord = centerSampleCoord;
        sampleCoord.xy += int(pc.sampleOffset) * normalizedOffsets[i];

        uvec2 seedCoord = imageLoad(pingPongImage, sampleCoord).xy;
        if (seedCoord != uvec2(0U)) {
            uint seedDistanceSq = length2(seedCoord - centerSampleCoord.xy);
            if (seedDistanceSq < closestSeedDistanceSq) {
                closestSeedDistanceSq = seedDistanceSq;
                closestSeedCoord = seedCoord;
            }
        }
    }

    ivec3 storeCoord = centerSampleCoord;
    storeCoord.z ^= int(gl_NumWorkGroups.z);
    imageStore(pingPongImage, storeCoord, uvec4(closestSeedDistanceSq == UINT_MAX ? uvec2(0) : closestSeedCoord, 0, 0));
}