#version 450
#include "cubemap.glsl"

const vec2 INV_ATAN = vec2(0.1591, 0.3183);

layout (set = 0, binding = 0) uniform sampler2D eqmapSampler;
layout (set = 0, binding = 1) writeonly uniform imageCube cubemapImage;

layout (local_size_x = 16, local_size_y = 16) in;

// --------------------
// Functions.
// --------------------

vec2 sampleSphericalMap(vec3 v){
    return INV_ATAN * vec2(atan(v.z, v.x), asin(v.y)) + 0.5;
}

void main(){
    // Load color from equirectangular map image.
    vec2 uv = sampleSphericalMap(getWorldDirection(gl_GlobalInvocationID, imageSize(cubemapImage).x));
    vec3 color = textureLod(eqmapSampler, uv, 0.0).rgb;

    // Store color into cubemapImage.
    imageStore(cubemapImage, ivec3(gl_GlobalInvocationID), vec4(color, 1.0));
}