#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_EXT_scalar_block_layout : require

#include "cubemap.glsl"

const ivec2 gatherOffsets[] = {
    { 0, 1 }, // i0_j1
    { 1, 1 }, // i1_j1
    { 1, 0 }, // i1_j0
    { 0, 0 }, // i0_j0
};

struct SphericalHarmonicBasis{
    float band0[1];
    float band1[3];
    float band2[5];
};

layout (set = 0, binding = 0) uniform sampler2DArray cubemapSampler;
layout (set = 0, binding = 1, scalar) writeonly buffer ReductionBuffer {
    vec3 coefficients[][9];
} reduction;

layout (local_size_x = 16, local_size_y = 16) in;

shared vec3 sharedData[16][9]; // For gl_SubgroupSize = 16 (minimum requirement)

// --------------------
// Functions.
// --------------------

float getComponent(vec4 v, uint i) {
    switch (i) {
        case 0U: return v.x;
        case 1U: return v.y;
        case 2U: return v.z;
        case 3U: return v.w;
    }
}

SphericalHarmonicBasis getSphericalHarmonicBasis(vec3 v){
    return SphericalHarmonicBasis(
        float[1](0.282095),
        float[3](-0.488603 * v.y, 0.488603 * v.z, -0.488603 * v.x),
        float[5](1.092548 * v.x * v.y, -1.092548 * v.y * v.z, 0.315392 * (3.0 * v.z * v.z - 1.0), -1.092548 * v.x * v.z, 0.546274 * (v.x * v.x - v.y * v.y))
    );
}

void main(){
    vec3 coefficients[9] = vec3[9](vec3(0), vec3(0), vec3(0), vec3(0), vec3(0), vec3(0), vec3(0), vec3(0), vec3(0));

    uint cubemapTextureSize = textureSize(cubemapSampler, 0).x;
    float cubemapTextureSizeRcp = 1.0 / float(cubemapTextureSize);
    vec2 globalInvocationId2 = 2 * vec2(gl_GlobalInvocationID.xy);
    vec4 solidAngles = vec4(
        texelSolidAngle(globalInvocationId2 + gatherOffsets[0], cubemapTextureSizeRcp),
        texelSolidAngle(globalInvocationId2 + gatherOffsets[1], cubemapTextureSizeRcp),
        texelSolidAngle(globalInvocationId2 + gatherOffsets[2], cubemapTextureSizeRcp),
        texelSolidAngle(globalInvocationId2 + gatherOffsets[3], cubemapTextureSizeRcp)
    );

    for (int faceIndex = 0; faceIndex < 6; ++faceIndex){
        vec3 gatherCoordinate = vec3((globalInvocationId2 + 1.0) * cubemapTextureSizeRcp, faceIndex);
        vec4 textureGatherRed = textureGather(cubemapSampler, gatherCoordinate, 0);
        vec4 textureGatherGreen = textureGather(cubemapSampler, gatherCoordinate, 1);
        vec4 textureGatherBlue = textureGather(cubemapSampler, gatherCoordinate, 2);

        for (uint i = 0; i < 4; ++i) {
            vec3 L = getComponent(solidAngles, i) * vec3(getComponent(textureGatherRed, i), getComponent(textureGatherGreen, i), getComponent(textureGatherBlue, i));
            ivec3 coordinate = ivec3(2 * gl_GlobalInvocationID.xy + gatherOffsets[i], faceIndex);
            SphericalHarmonicBasis basis = getSphericalHarmonicBasis(getWorldDirection(coordinate, cubemapTextureSize));

            coefficients[0] += L * basis.band0[0];
            coefficients[1] += L * basis.band1[0];
            coefficients[2] += L * basis.band1[1];
            coefficients[3] += L * basis.band1[2];
            coefficients[4] += L * basis.band2[0];
            coefficients[5] += L * basis.band2[1];
            coefficients[6] += L * basis.band2[2];
            coefficients[7] += L * basis.band2[3];
            coefficients[8] += L * basis.band2[4];
        }
    }

    vec3 subgroupCoefficients[9] = vec3[9](
        subgroupAdd(coefficients[0]),
        subgroupAdd(coefficients[1]),
        subgroupAdd(coefficients[2]),
        subgroupAdd(coefficients[3]),
        subgroupAdd(coefficients[4]),
        subgroupAdd(coefficients[5]),
        subgroupAdd(coefficients[6]),
        subgroupAdd(coefficients[7]),
        subgroupAdd(coefficients[8])
    );
    if (subgroupElect()){
        sharedData[gl_SubgroupID] = subgroupCoefficients;
    }

    memoryBarrierShared();
    barrier();

    if (gl_SubgroupID == 0U){
        bool isInvocationInSubgroup = gl_SubgroupInvocationID < gl_NumSubgroups;
        coefficients = vec3[9](
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][0] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][1] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][2] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][3] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][4] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][5] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][6] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][7] : vec3(0.0)),
            subgroupAdd(isInvocationInSubgroup ? sharedData[gl_SubgroupInvocationID][8] : vec3(0.0))
        );
    }

    if (gl_LocalInvocationID.xy == uvec2(0)){
        // TODO: why reduction.coefficients[gl_WorkGroupID.y * gl_NumWorkGroups.x + gl_WorkGroupID.x] = coefficients; doesn't work?
        for (uint i = 0; i < 9U; ++i){
            reduction.coefficients[gl_WorkGroupID.y * gl_NumWorkGroups.x + gl_WorkGroupID.x][i] = coefficients[i];
        }
    }
}